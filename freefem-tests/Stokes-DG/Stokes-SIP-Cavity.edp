// Discontinous Galerlin SIP Method for solve Stokes system
//
// Based on [Di Pietro - Ern, Mathematical Aspects of Discontinuous
// Galerkin Methods], Chapter 6
//
// J. Rafael Rodr\'iguez Galv\'an

////////////////
// Parameters //
////////////////

verbosity=0;
int interactive=1; // If 1: plot results

real SIPPenalty=1.0e+4; // Penalty to achieve coercivity in DG SIP forms
real BoundaryPenalty=3.0e+2; // Penalty for Nitsche method (weak Dirichlet condition)
real nu=1.0;

// macro hFace() (1.0/sqrt(n)) // An approximation of h_F (length of face)
macro hFace() (lenEdge) // An approximation of h_F (length of face)
  ;
macro LocalPenalty() (SIPPenalty/hFace) // EOM
  ;

func uSurface = x*(1-x); // Horizontal surface velocity in cavity test
// func uSurface=1;
func f1 = 0; // RHS / u
func f2 = 0; // RHS / v
real pEpsilon  = 1.0e-10; // Penalty epsilon in p-equations

int [int] meshSizes= [48];

for(int i=0; i<meshSizes.n; i++) {
  int n = meshSizes[i];
  cout << "n=" << n << endl;

  ////////////////////////////////////
  // Mesh and Finite Element Spaces //
  ////////////////////////////////////

  // Structured mesh
  // mesh Th = square(n,n);
  // int bottom=1, surface=3; int [int] walls = [2,4];
  // Unstructured mesh
  real domainX0=0, domainX1=1, domainY0=0, domainY1=1;
  real ix=domainX1-domainX0, iy=domainY1-domainY0;
  int walls=100;
  border bottom(t=0,ix) { x=domainX0+t; y=domainY0;   }
  border rWall(t=0,iy)  { x=domainX1;   y=domainY0+t; label=walls;}
  border surface(t=0,ix){ x=domainX1-t; y=domainY1;   }
  border lWall(t=0,iy)  { x=domainX0;   y=domainY1-t; label=walls;}

  mesh Th=buildmesh( bottom(n) + rWall(n)
		     + surface(n) + lWall(n) );

  int k = 1; // Degree of the polynomials (2 for P2dc ...)
  assert(k==1); // Assure to change the following lines...
  func PkDGu = P1dc;  // Discontinuous Pk for pressure
  func PkDGp = P1dc;  // Discontinuous Pk for pressure
  func PkCGu = P1b;  // Continous Pk for velocity
  func PkCGp = P1;  // Continous Pk for pressure
  int qfo = 2*k;  // Order of quadrature formula
  int qfoP = qfo;  // Order of quadrature formula for pressure integrals

  // Discontinuous FE
  fespace UhDisc(Th,PkDGu);
  fespace VhDisc(Th,PkDGu);
  fespace PhDisc(Th,PkDGp);
  UhDisc u, ub;
  VhDisc v, vb;
  PhDisc p, pb;

  // Continuous FE (for testing)
  fespace UhCont(Th,PkCGu);
  fespace VhCont(Th,PkCGu);
  fespace PhCont(Th,PkCGp);
  UhCont uu, uub;
  VhCont vv, vvb;
  PhCont pp, ppb;

  ////////////
  // Macros //
  ////////////

  macro dn(u) (N.x*dx(u)+N.y*dy(u)) //  Normal derivative
    ;
  macro div(u,v) (dx(u)+dy(v)) // EOM
    ;
  // def: jump = external - internal value; on border exter value =0
  //      average = (external + internal value)/2, on border just internal value
  NewMacro aSIP(u,ub)
    (
     int2d(Th) ( nu * ( dx(u)*dx(ub) + dy(u)*dy(ub) ) )
     + intalledges(Th)
     (
      - nu * ( average(dn(u))*jump(ub) + average(dn(ub))*jump(u) ) / nTonEdge
      + (LocalPenalty)*(jump(u)*jump(ub)) / nTonEdge
      )
     ) EndMacro;
  NewMacro bSIP1(u,v,p)
    int2d(Th)( -p*(div(u,v)) )
    EndMacro;
  NewMacro bSIP2(u,v,p)
    intalledges(Th)
    (
     (jump(u)*N.x + jump(v)*N.y)*average(p) / nTonEdge
     ) EndMacro;
  NewMacro sSIP(q,r)
    intalledges(Th)
    (
     (nTonEdge-1)*hFace*jump(q)*jump(r)
     ) EndMacro;

  //////////////////////////////
  // Variational Formulations //
  //////////////////////////////

  problem StokesSIP([u,v,p], [ub,vb,pb]) =
    // 1) Bilinear form (mount all terms)
    aSIP(u,ub) + aSIP(v,vb) // Bilinear form a(.,.)
    + bSIP1(ub,vb,p) + bSIP2(ub,vb,p) // Bilinear form b(.,.)
    - bSIP1(u,v,pb) - bSIP2(u,v,pb) // Bilinear form b(.,.)
    + sSIP(p, pb) // Stabilization
    + int2d(Th)(pEpsilon*p*pb)
    // //
    // // RHS: Weak boundary conditions...
    // //
    // - int1d(Th, surface) (
    //  -uSurface*dn(ub)
    //  )
    // - int1d(Th, surface)
    // (
    //  (LocalPenalty)*uSurface*ub
    //  )
    //
    // 2) Bilinear form a(.,.): remove Dirichlet or Neumann b.c. if not imposed
    //
    // 2.1) u on surface, walls & bottom: Dirichlet b.c. (eliminate Neumann terms)
    - int1d(Th, surface) (nu * dn(u)*ub)
    - int1d(Th, walls, bottom) (nu * dn(u)*ub)
    // 2.2) v on surface & bottom: Dirichlet b.c. (eliminate Neumann terms)
    - int1d(Th, surface, bottom) ( nu * dn(v)*vb)
    // 2.3) v on walls: Neumann b.c. (eliminate Dirichlet terms)
    - int1d(Th, walls)  ( nu * v*dn(vb)
    			  - LocalPenalty* v*vb )
    //
    // 3) Boundary conditions from a(.,.)
    //
    // 3.1) u on surface, walls & bottom: Dirichlet b.c.
    - int1d(Th, surface) (nu * uSurface*dn(ub)
    			  - LocalPenalty * uSurface*ub)
    - int1d(Th, walls, bottom) (nu * 0.0*dn(ub)
    				- LocalPenalty * 0*ub)
    // 2.a.2) v on surface & bottom: Dirichlet b.c.
    - int1d(Th, surface, bottom) ( nu * 0.0*dn(vb)
    				   - LocalPenalty * 0*vb )
    // 2.a.2) v on walls: Neumann b.c.
    - int1d(Th, walls) (0*vb)
    // - int1d(Th, surface) // - bSIP2(u,v,pb)
    // (
    //  - uSurface*N.x*pb // =0 because N.x=0 on surface
    // )
    ;
    // + on(bottom, walls,  u=0, v=0)
    // + on(surface, u=uSurface, v=0);

  problem Stokes([uu,vv,pp], [uub,vvb,ppb]) =
    int2d(Th) (
	       + nu * ( dx(uu)*dx(uub) + dy(uu)*dy(uub) )
	       + nu * ( dx(vv)*dx(vvb) + dy(vv)*dy(vvb) )
	       - pp*dx(uub) - pp*dy(vvb)
	       + dx(uu)*ppb + dy(vv)*ppb
	       // + pEpsilon*pp*ppb
	       )
    // + int1d(Th, surface, bottom, walls)
    // (
    //  - nu*(dn(uu)*uub + dn(vv)*vvb)
    //  )
    // + int1d(Th, surface, bottom, walls)
    // (
    //  - nu*(uu*dn(uub) + vv*dn(vvb))
    //  + BoundaryPenalty/hFace*(uu*uub + vv*vvb)
    //  )
    // // RHS //
    // + int1d(Th, surface)
    // (
    //  + nu*uSurface*dn(uub)
    //  - (BoundaryPenalty/hFace)*uSurface*uub
    //  )
    + on(bottom, uu=0, vv=0)
    + on(walls, uu=0)
    + on(surface, uu=uSurface, vv=0);
    ;

  //////////////
  // Solve DG //
  //////////////
  real time0, time1;
  time0 = clock();
  StokesSIP;
  real pMean=int2d(Th)(p)/int2d(Th)(1.0);
  p=p-pMean;
  time1 = clock();
  cout << "DG SIP time: " << time1-time0 << endl;
  // plot(p,cmm="DG-mesh_size=" + n,wait=1,value=1,fill=1);

  //////////////
  // Solve CG //
  //////////////
  time0 = clock();
  Stokes;
  pMean=int2d(Th)(p)/int2d(Th)(1.0);
  p=p-pMean;
  time1 = clock();
  cout << "CG time:     " << time1-time0 << endl;

  real divDG = int2d(Th)(div(u,v)), divCG = int2d(Th)(div(uu,vv));
  cout << " Divergence (integral in L2)... CG/DG: " << divCG << " / " << divDG << endl;
  real kinDG = sqrt(int2d(Th)(square(u)+square(v)));
  real kinCG = sqrt(int2d(Th)(square(uu)+square(vv)));
  cout << " Kinetic Energy................ CG/DG: " << kinCG << " / " << kinDG << endl;
  real pDG = int2d(Th)(p);
  real pCG = int2d(Th)(pp);
  cout << " Pressure integral....... CG/DG: " << pCG << " / " << pDG << endl;

  if(true) {
    load "iovtk";
    savevtk("/tmp/DG-Stokes-cavity" + n + ".vtk",
	    Th,
	    [u,v,0],
	    p,
	    dataname="Velocity_DG Pressure_DG") ;
    savevtk("/tmp/CG-Stokes-cavity" + n + ".vtk",
	    Th,
	    [uu,vv,0],
	    pp,
	    dataname="Velocity_CG Pressure_CG") ;
  }

  //////////////
  // Solve CG //
  //////////////
  time0 = clock();
  Stokes;
  time1 = clock();
  cout << "CG time:     " << time1-time0 << endl;

  if(interactive) plot(p,[u,v], cmm="DG pressure-mesh_size=" + n ,wait=1,value=1,fill=1);
  if(interactive) plot(u, cmm="DG, u, pressure-mesh_size=" + n ,wait=1,value=1,fill=1);
  if(interactive) plot(v, cmm="DG, v, pressure-mesh_size=" + n ,wait=1,value=1,fill=1);
  if(interactive) plot(pp,[uu,vv], cmm="CG pressure-mesh_size=" + n ,wait=1,value=1,fill=1);
  if(interactive) plot(uu, cmm="CG, u, pressure-mesh_size=" + n ,wait=1,value=1,fill=1);
  if(interactive) plot(vv, cmm="CG, v, pressure-mesh_size=" + n ,wait=1,value=1,fill=1);

  // cout << "Residual (DG): " << int2d(Th,qforder=qfo)(dx(u)*dx(u) +
  // dy(u)*dy(u) - int2d(Th)(f*u)) << endl;
  // cout << "Residual (CG): " << int2d(Th)(dx(uu)*dx(uu) +
  // dy(uu)*dy(uu) - int2d(Th)(f*uu)) << endl;

  // Vh error = u-uExact;
 }
