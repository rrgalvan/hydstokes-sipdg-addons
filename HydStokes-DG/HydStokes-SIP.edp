// Discontinous Galerlin SIP Method for solve Stokes system
//
// Based on [Di Pietro - Ern, Mathematical Aspects of Discontinuous
// Galerkin Methods], Chapter 6
//
// J. Rafael Rodr\'iguez Galv\'an
// load "MUMPS_FreeFem"

load "Element_P3"
load "Element_P4"
load "Element_P3dc"
load "Element_P4dc"

////////////////
// Parameters //
////////////////

verbosity=0;
int interactive=1; // If 1: plot results

real PenaltyConstant=5.0e+3; // DG Penalty (to achieve coercivity)
real nu=1.; // Horizontal viscosity coefficient

real domainEpsilon = 1.e-1; // Domain Epsilon
real dEpsilon2 = domainEpsilon*domainEpsilon;

// macro hFace() (1.0/sqrt(n)) // An approximation of h_F (length of face)
macro hFace() (lenEdge) // An approximation of h_F (length of face)
  ;
macro LocalPenalty() (PenaltyConstant/hFace) // EOM
  ;

int [int] meshSizes= [8,16,32];

include "exact-sol-HydStokes-steady.idp"
func f1 = f1Exact; // RHS / u
func f2 = f2Exact; // RHS / v
real pEpsilon  = 1.0e-10; // Penalty epsilon in p-equations

for(int i=0; i<meshSizes.n; i++) {
  int n = meshSizes[i];
  cout << "n=" << n << endl;

  ////////////////////////////////////
  // Mesh and Finite Element Spaces //
  ////////////////////////////////////
  mesh Th = square(n,n);
  int bottom=1, surface=3; int [int] walls = [2,4];

  int k = 1; // Degree of the polynomials (2 for P2dc ...)
  assert(k==1); // Assure to change the following lines...
  func PkDG = P1dc;  // Discontinuous Pk
  func PkCGu = P1b;  // Continous Pk for velocity
  func PkCGp = P1;  // Continous Pk for pressure
  int qfo = 2*k;  // Order of quadrature formula
  int qfoV = qfo+1;  // Order of quadrature formula for v integrals
  int qfoP = qfo+1;  // Order of quadrature formula for pressure integrals

  // Discontinuous FE
  fespace UhDisc(Th,PkDG);
  fespace VhDisc(Th,PkDG);
  fespace PhDisc(Th,PkDG);
  UhDisc u, ub;
  VhDisc v, vb;
  PhDisc p, pb;

  // Continuous FE (for testing)
  fespace UhCont(Th,PkCGu);
  fespace VhCont(Th,PkCGu);
  fespace PhCont(Th,PkCGp);
  UhCont uu, uub;
  VhCont vv, vvb;
  PhCont pp, ppb;

  ////////////
  // Macros //
  ////////////

  macro dn(u) (N.x*dx(u)+N.y*dy(u) ) //  Normal derivative
    ;
  macro div(u,v) (dx(u)+dy(v)) // EOM
    ;
  // def: jump = external - internal value; on border exter value =0
  //      average = (external + internal value)/2, on border just internal value
  NewMacro aUSIP(u,ub)
    (
     int2d(Th, qforder=qfo) ( nu * ( dx(u)*dx(ub) + dy(u)*dy(ub) ) )
     + intalledges(Th, qforder=qfo)
     (
      ( jump(ub)*average(dn(u)) + jump(u)*average(dn(ub)) ) /nTonEdge
      + LocalPenalty*(jump(u)*jump(ub))
      )
     ) EndMacro;
  NewMacro aVSIP(v,vb)
    (
     int2d(Th, qforder=qfo) ( nu*dEpsilon2 * ( dx(v)*dx(vb) + dy(v)*dy(vb) ) )
     + intalledges(Th, qforder=qfoV)
     (
      // dEpsilon2*PenaltyParameter*(jump(v)*jump(vb)) /nTonEdge
      dEpsilon2*( jump(vb)*average(dn(v)) + jump(v)*average(dn(vb)) ) /nTonEdge
      + LocalPenalty*(jump(v)*jump(vb))
      )
     ) EndMacro;
  NewMacro bSIP1(u,v,p)
    int2d(Th, qforder=qfo)( -p*(div(u,v)) )
    EndMacro;
  NewMacro bSIP2(u,v,p)
    intalledges(Th, qforder=qfo)
    (
     (jump(u)*N.x + jump(v)*N.y)*average(p) /nTonEdge
     ) EndMacro;
  NewMacro sSIP(q,r)
    intalledges(Th, qforder=qfoP)
    (
     (nTonEdge-1)*hFace*jump(q)*jump(r)
     ) EndMacro;

  //////////////////////////////
  // Variational Formulations //
  //////////////////////////////

  problem HydStokesSIP([u,v,p], [ub,vb,pb]) =
    aUSIP(u,ub) + aVSIP(v,vb) // Bilinear form a(.,.)
    + bSIP1(ub,vb,p) + bSIP2(ub,vb,p) // Bilinear form b(.,.)
    - bSIP1(u,v,pb) - bSIP2(u,v,pb) // Bilinear form b(.,.)
    + sSIP(p, pb) // Stabilization
    + int2d(Th)(pEpsilon*p*pb)
    - int2d(Th)
    (
     + f1(x,y) * ub
     + f2(x,y) * vb
     )
    + int1d(Th, bottom, walls, surface)
    ( // Dirichlet condition (imposee de maniere faible)
     uExact(x,y)*(dn(ub)-LocalPenalty*ub)
     + dEpsilon2*vExact(x,y)*(dn(vb)-LocalPenalty*vb)
     );
    // + on(bottom,  u=uExact(x,y), v=vExact(x,y))
    // + on(walls,   u=uExact(x,y), v=vExact(x,y))
    // + on(surface, u=uExact(x,y), v=vExact(x,y));

  problem HydStokes([uu,vv,pp], [uub,vvb,ppb]) =
    int2d(Th) (
	       + nu * ( dx(uu)*dx(uub) + dy(uu)*dy(uub) )
	       + nu*dEpsilon2 * ( dx(vv)*dx(vvb) + dy(vv)*dy(vvb) )
	       - pp*dx(uub) - pp*dy(vvb)
	       + dx(uu)*ppb + dy(vv)*ppb
	       + nu*div(uu,vv)*dy(vvb) // dz(v) stabilization term
	       + pEpsilon*pp*ppb
	       )
    - int2d(Th) (
		 + f1(x,y) * uub
		 + f2(x,y) * vvb
		 )
    // + int1d(Th, bottom, walls, surface)
    // (
    //  uExact(x,y)*(dn(uub)-PenaltyParameter*uub) // Dirichlet condition (imposee de maniere faible)
    //  + vExact(x,y)*(dn(vvb)-PenaltyParameter*vvb) // Dirichlet condition (imposee de maniere faible)
    //  );
    + on(bottom,  uu=uExact(x,y), vv=vExact(x,y))
    + on(walls,   uu=uExact(x,y), vv=vExact(x,y))
    + on(surface, uu=uExact(x,y), vv=vExact(x,y));

  //////////////
  // Solve DG //
  //////////////
  real time0, time1;
  time0 = clock();
  HydStokesSIP;
  // Force zero mean
  bool forceZeroMean=true; if(forceZeroMean) {
    PhDisc one; one[] = 1.;
    real pMean = int2d(Th)(p)/int2d(Th)(one);
    p = p - pMean;
  }
  time1 = clock();
  cout << "DG SIP time: " << time1-time0 << " << [SIP-DG]"<< endl;
  if(interactive) plot(p,cmm="Discontinuous Galerkin",wait=1,value=1,fill=1);

  //////////////
  // Solve CG //
  //////////////
  time0 = clock();
  HydStokes;
  time1 = clock();
  cout << "CG time:     " << time1-time0 << endl;
  if(interactive) plot(pp,cmm="Continuous Galerkin",wait=1,value=1,fill=1);

  // cout << "Residual (DG): " << int2d(Th,qforder=qfo)(dx(u)*dx(u) +
  // dy(u)*dy(u) - int2d(Th)(f*u)) << endl;
  // cout << "Residual (CG): " << int2d(Th)(dx(uu)*dx(uu) +
  // dy(uu)*dy(uu) - int2d(Th)(f*uu)) << endl;

  // Vh error = u-uExact;
  real uError = sqrt(int2d(Th)(square(dx(u)-DXuExact(x,y))));
  real vError = sqrt(int2d(Th)(square(dy(v)-DYvExact(x,y))));
  real pError = sqrt(int2d(Th)(square(p-pExact(x,y))));
  cout << "DG u,v/p error L2: " << uError << "," << vError << "/" << pError << "<<***" << endl;
  real uuError = sqrt(int2d(Th)(square(dx(uu)-DXuExact(x,y))));
  real vvError = sqrt(int2d(Th)(square(dy(vv)-DYvExact(x,y))));
  real ppError = sqrt(int2d(Th)(square(pp-pExact(x,y))));
  cout << "CG u,v/p error L2: " << uuError << "," << vvError << "/" << ppError << endl;

  bool saveVTK=false; if(saveVTK) {
    load "iovtk";
    savevtk("/tmp/DGsolution" + n + ".vtk",
	    Th,
	    [u,v,0],
	    p,
	    dataname="Velocity Pressure") ;
    savevtk("/tmp/CGsolution" + n + ".vtk",
	    Th,
	    [uu,vv,0],
	    pp,
	    dataname="Velocity Pressure") ;
    savevtk("/tmp/EXACTsolution" + n + ".vtk",
	    Th,
	    [uExact(x,y),vExact(x,y),0],
	    pExact(x,y),
	    dataname="ExactV ExactP") ;
  }
 }
